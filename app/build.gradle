plugins {
    id("com.android.application")
}

android {
    namespace = "fr.corentin.roux.x_wing_score_tracker"
    compileSdk = 36
    ndkVersion = "29.0.14206865"
    buildToolsVersion = '36.0.0'

    defaultConfig {
        applicationId = "fr.corentin.roux.score_tracker"
        minSdkVersion 24
        targetSdkVersion 36
        versionCode = VERSION_CODE.toInteger()
        versionName = VERSION_MAJEUR + "." + VERSION_MINEUR + "." + VERSION_PATCH
        multiDexEnabled = true
        ndk {
            // Filter for architectures supported by Flutter.
            abiFilters 'armeabi-v7a' , 'arm64-v8a' , 'x86_64' , 'x86'
            debugSymbolLevel = 'FULL'
        }
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled = true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources = true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            debuggable = false
        }

        debug {
            debuggable = true
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding = true
    }

    //apply this at the end of the file
    applicationVariants.configureEach { variant ->
        def buildType = variant.buildType.name
        //increment only for 'release' builds
        if ('release' == buildType) {
            def versionCode = VERSION_CODE.toInteger()
            versionCode += 1
            ant.propertyfile(file: "../gradle.properties") {
                entry(key: "VERSION_CODE", value: versionCode)
            }

            def versionPatch = VERSION_PATCH.toInteger()
            versionPatch += 1
            ant.propertyfile(file: "../gradle.properties") {
                entry(key: "VERSION_PATCH", value: versionPatch)
            }
        }
    }
}

dependencies {
//    implementation 'com.google.android.play:integrity:1.4.0'
    implementation("com.google.android.material:material:1.14.0-alpha09")
    // https://mvnrepository.com/artifact/io.vavr/vavr
    implementation("io.vavr:vavr:1.0.0")
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation("com.google.code.gson:gson:2.13.2")
    // Android X
    implementation("androidx.constraintlayout:constraintlayout:2.2.1")
    def navigation_version = "2.9.7"
    implementation("androidx.navigation:navigation-ui:" + navigation_version)
    implementation("androidx.navigation:navigation-fragment:" + navigation_version)
    implementation("androidx.multidex:multidex:2.0.1")
    implementation("androidx.appcompat:appcompat:1.7.1")
    def room_version = "2.8.4"
    implementation("androidx.room:room-runtime:" + room_version)
    annotationProcessor("androidx.room:room-compiler:" + room_version)
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation("androidx.room:room-ktx:" + room_version)
    // optional - RxJava2 support for Room
    implementation("androidx.room:room-rxjava2:" + room_version)
    // optional - RxJava3 support for Room
    implementation("androidx.room:room-rxjava3:" + room_version)
    // optional - Guava support for Room, including Optional and ListenableFuture
    implementation("androidx.room:room-guava:" + room_version)
    // optional - Paging 3 Integration
    implementation("androidx.room:room-paging:" + room_version)
}
